plugins {
	id 'mod_conventions_loader'
	alias libs.plugins.fabric.loom
}

repositories {
	maven { url = 'https://maven.terraformersmc.com/releases' }
}

base.archivesName = 'ok_zoomer-fabric'

// All the dependencies are declared at gradle/libs.version.toml and referenced with "libs.<id>"
// See https://docs.gradle.org/current/userguide/platforms.html for information on how version catalogs work.
dependencies {
	minecraft libs.minecraft
	mappings(loom.layered {
		it.officialMojangMappings()
		it.parchment libs.parchment
	})
	modImplementation libs.fabric.loader

	modImplementation libs.fabric.api

	modCompileOnly libs.modmenu
	modLocalRuntime libs.modmenu

	implementation libs.wrench.wrapper

	modCompileOnly libs.accessories.fabric
	modLocalRuntime libs.accessories.fabric

	include libs.wrench.wrapper
}

loom {
	def atFile = project(':mod_common').file("src/main/resources/${mod_id}.accesswidener")
	if (atFile.exists()) {
		accessWidenerPath.set(atFile)
	}

	mixin {
		useLegacyMixinAp = false
	}

	runs {
		named('client') {
			client()
			configName = 'Fabric Client'
			ideConfigGenerated(true)
			runDir('run')
		}
	}

	mods {
		"${mod_id}" {
			sourceSet sourceSets.main
		}
	}
}

processResources {
	it.filteringCharset 'UTF-8'

	def expandProps = [
		'version'                       : project.version,
		'curseforge_id'					: project.curseforge_id,
		'modrinth_id'					: project.modrinth_id,
	]
	it.inputs.properties(expandProps)

	it.filesMatching('fabric.mod.json') {
		it.expand expandProps
	}
}
