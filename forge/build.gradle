plugins {
	id 'mod_conventions_loader'
	alias libs.plugins.moddevgradle
}

base.archivesName = 'ok_zoomer-forge'

repositories {
	maven { url = 'https://maven.neoforged.net/releases' }
}

processResources {
	it.filteringCharset 'UTF-8'

	def expandProps = [
		'version'                       : project.version,
		'curseforge_id'					: project.curseforge_id,
		'modrinth_id'					: project.modrinth_id,
	]
	it.inputs.properties(expandProps)

	it.filesMatching('META-INF/mods.toml') {
		it.expand expandProps
	}
}

legacyForge {
	version = libs.versions.forge.get()

	parchment {
		minecraftVersion = libs.versions.minecraft.get()
		mappingsVersion = libs.versions.parchment.get()
	}

	runs {
		client {
			client()
		}
	}

	mods {
		ok_zoomer {
			sourceSet(sourceSets.main)
		}
	}
}

dependencies {
	annotationProcessor variantOf(libs.sponge) { classifier("processor") }
	annotationProcessor libs.mixinextras.common

	implementation libs.wrench.wrapper
	jarJar libs.wrench.wrapper

	compileOnly libs.mixinextras.common
	implementation libs.mixinextras.forge
	jarJar libs.mixinextras.forge
}

mixin {
	add sourceSets.main, 'ok_zoomer_forge.mixins.refmap.json'
	config "ok_zoomer_forge.mixins.json"
}

jar {
	manifest.attributes([
	    "MixinConfigs": "ok_zoomer_forge.mixins.json"
	])
}
